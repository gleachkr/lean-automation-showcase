import Duper
import Aesop
import Canonical
import Smt
import Smt.Real
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Basic
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Deriv

open Real

/- 
SLIDE 


Fun with lean automation
========================


FIN 
-/



/- 
SLIDE

Intro

Lean is a functional programming language.

Everything is an expression, there are no statements

FIN 
-/

/- SLIDE -/

/-

On the surface, it looks a bit like Haskell or OCaml.

You've got ADTs:

-/

inductive Workday  /- VS: "data Workdays = " -/
| Monday
| Tuesday
| Wednesday
| Thursday
| Friday

/- SLIDE -/

/-

Pattern Matching:

(constructors are automatically namespaced by type, unlike Haskell)

-/

def feeling : Workday â†’ String
| Workday.Monday => "ugh!"
| Workday.Friday => "yay!"
| _ => "ðŸ˜"

/- FIN -/

/- SLIDE -/

/-

Standard higher order stuff, Polymorphism:

-/

#check List.map

#eval [1,2,3].map (Î»x => x + 1)

/-

Though, namespaces are used to provide a "method-like" syntax for some
function calls, so it looks a little like JavaScript 

-/

/- SLIDE -/

/- Recursive definitions are as you'd expect -/

def myPlus : Nat â†’ Nat â†’ Nat
| 0 , m => m
| .succ n, m => .succ (myPlus n m)

def myPlusAlt : Nat â†’ Nat â†’ Nat
| m , 0 => m
| n, .succ m => .succ (myPlusAlt n m)

/- SLIDE -/

/- 

And you've got monads, the haskell syntactic sugar for composing together terms
that have side-effects (broadly understood) when evaluated 

-/

def hello_world := do
  let x â† pure "hello"
  let y â† pure "world!"
  IO.print (x ++ " " ++ y)

#eval hello_world

/- SLIDE -/


/- Lean also has *dependent types* -/

inductive Vec : Nat â†’ Type
| nil : Vec 0
| cons : Nat â†’ Vec n â†’ Vec (.succ n)

#check Vec.nil

#check Vec.cons 1 Vec.nil

def nvec : (n : Nat) â†’ Vec n 
| 0 => Vec.nil
| n + 1 => Vec.cons 0 (nvec n)

/- And with dependent types, there's a crazy thing you can do -/

/- SLIDE -/

def kants_constant : myPlus 5 7 = myPlusAlt 5 7 := Eq.refl 12

/- SLIDE -/

/- in other words -/


theorem kants_theorem : myPlus 5 7 = myPlusAlt 5 7 := Eq.refl 12

/- SLIDE -/

/- More substantial theorems are possible. -/

theorem plus_equiv : (x y : Nat) â†’ myPlus x y = myPlusAlt x y 
|     0,     0 => Eq.refl 0
| z + 1,     0 => by
  unfold myPlus
  rw [plus_equiv z 0]
  rfl
|     0, y + 1 => by 
  unfold myPlusAlt
  rw [â†plus_equiv 0 y]
  rfl
| x + 1, y + 1 => by
  unfold myPlus myPlusAlt
  rw [plus_equiv x (y + 1), â†plus_equiv (x + 1) y] 
  unfold myPlus myPlusAlt
  rw [plus_equiv x y] 


theorem plus_equiv' : âˆ€(x y : Nat), myPlus x y = myPlusAlt x y := plus_equiv

/- SLIDE -/

/- SLIDE

"It is unworthy of excellent people to lose hours like slaves in the labour of
calculation which could safely be relegated to anyone if machines were used." 

- Machina arithmetica in qua non additio tantum et subtractio sed et
  multiplicatio nullo, divisio vero paene nullo animi labore peragantur
  (Leibniz)

FIN -/

/- SLIDE -/

/-

Simp: extensible term rewriting guided by simp lemmas, uses proven equalities
and equivalences to try to prove a theorem by simplifying some of the expressions involved

-/

example (a b c : Nat) : (a + b) + c = a + (b + c) := by simp +arith

example : deriv (Î»x => cos (sin x)) x =  (sin âˆ˜ sin) x * (- cos x) := by simp

example (a b : Nat) : (a = b + 1 âˆ¨ b = a + 1) â†’ (a > b âˆ¨ b > a) := by
  simp +arith

/- SLIDE -/

/- omega, linarith, ring, bv_decide ... -/

example (x y : Real) : x^3 - y^3 = (x-y) * (x^2+x*y+y^2) := by ring

example : deriv (Î»x => cos (sin x) * exp x) x = (cos (sin x) - sin (sin x) * cos x) * exp x := by simp; ring

example : âˆ€(i  : UInt32),
  i = (i >>> 0x18 % 256) <<< 0x18 |||
      (i >>> 0x10 % 256) <<< 0x10 |||
      (i >>> 0x08 % 256) <<< 0x08 |||
      i % 256 := by bv_decide 
      /- This is surprisingly tedious to prove by hand! -/

/- SLIDE -/

/-

Aesop
=====

* proof search by tunable heuristics, working at the level of lean tactics
proofs. Generally performs proof construction steps that increase the amount
of available information without throwing things away, and ultimately attempts
to use simp to solve things.

-/

theorem know_by_cases' (a b : Nat) : (a = b + 1 âˆ¨ b = a + 1) â†’ (a > b âˆ¨ b > a) := by aesop?

theorem know_by_cases'' (a b : Nat) : (a = b + 1 âˆ¨ b = a + 1) â†’ (a â‰  b) := by aesop?

/- It's limited when it comes to constructing new terms though -/

theorem something_bigger (a : Nat) : âˆƒb, b > a := by aesop

/- SLIDE -/

/- 

duper 

* backend is a superposition prover for higher-order logic, written in lean
  (negates conclusion, generates clauses looking for a contradiction)
  therefore classical

* Uses lean-auto to translate between higher-order logic and dependent type
  theory (can also do its own translations, but works best as a backend to
  lean-auto).

-/


theorem Grelling's_paradox : âˆ€Denotes: Î± â†’ Î± â†’ Prop, Â¬âˆƒy, âˆ€x, Â¬Denotes x x â†” Denotes y x := by duper

/- set_option trace.duper.printProof true in -/
theorem Prior's_paradox : âˆ€T : Prop â†’ Prop, T (âˆ€p, T p â†’ Â¬ p) â†’ (âˆƒp, T p âˆ§ Â¬p) âˆ§ (âˆƒp, T p âˆ§ p) := by duper

/- It's best at logical goals. Not so good at constructing witnesses. -/

theorem something_bigger' (a : Nat) : âˆƒb, b > a := by duper

/- SLIDE -/

/- canonical: constructs terms

* does a kind of guided breadth-first search (kind of like aesop), so that
  proof terms tend to be quite small (251Ã— smaller on average in some testing)

* unlike Aesop, it works at the term level, not at the tactic level, so it
  can't itself involve tactics like simp.

* aspires to *completeness* for dependent type theory - it's in fact a rust
  program, in principle separable from lean.

quite good at finding witnesses

-/

theorem something_bigger'' (a : Nat) : âˆƒb, b > a := by canonical

/- SLIDE -/

example : DecidableEq Î± â†’ (âˆƒx y : Î±, x â‰  y) â†’ âˆ€f : Î± â†’ (Î± â†’ Î±), âˆƒg : Î± â†’ Î±, Â¬âˆƒx : Î±, f x = g := by
  intros dec neqâ‚ f
  have âŸ¨x,y,hypâŸ© := neqâ‚ 
  let g := Î»w => if f w w = x then y else x
  exists g
  rintro âŸ¨w, neqâŸ©
  have : f w w = g w := by duper [*]
  cases Classical.em (f w w = x)
  case inl h => 
    have legâ‚ : g w = x := by canonical
    have legâ‚‚ : g w = y := by canonical
    duper [*]
  case inr h => 
    have legâ‚ : Â¬g w = x := by canonical
    have legâ‚‚ : g w = x := by canonical
    exact legâ‚ legâ‚‚

/- SLIDE -/

/- But canonical is not strictly better than duper. The examples below time out -/

/- example : âˆ€Denotes : Î± â†’ Î± â†’ Prop, Â¬âˆƒy, âˆ€x, Denotes x x â†” Â¬Denotes y y := 
  by canonical -/

/- example : âˆ€T : Prop â†’ Prop, T (âˆ€p, T p â†’ Â¬ p) â†’ (âˆƒp, T p âˆ§ Â¬p) âˆ§ (âˆƒp, T p âˆ§ p) := by canonical -/

/- SLIDE -/

/- 

Interestingly, canonical can also be used for program synthesis. Use an LSP
code action to replace the canonical invocation with the witnessing term it
found 

-/

def append : Vec n â†’ Vec m â†’ Vec (n + m) := by 
  canonical

#eval append (Vec.cons 5 Vec.nil) (Vec.cons 5 Vec.nil)

/- SLIDE -/

inductive PolyVec (Î± : Type) : Nat â†’ Type
| nil : PolyVec Î± 0
| cons : Î± â†’ PolyVec Î± n â†’ PolyVec Î± (n + 1)

/- Program syntesis is most interesting when dealing with polymorphic types which -/
/- might have only one inhabitant, the one you'd naturally think of -/

def PolyAppend : PolyVec Î± n â†’ PolyVec Î± m â†’ PolyVec Î± (n + m) := by 
  canonical

/- SLIDE -/

/- Lean-SMT 

* translates lean goals into SMT solver queries, and translates SMT solver
outputs back into lean proofs. 

* currently supports theory of uninterpreted functions, and real number arithmetic.

-/

example (Îµ : Real) (h1 : Îµ > 0) : Îµ / 2 + Îµ / 3 + Îµ / 7 < Îµ := by
  smt [h1]

example (Îµ Î´ : Real) (h : Îµ â‰  Î´): âˆƒr : Real, (r < Î´ âˆ§ Îµ < r) âˆ¨ (r < Îµ âˆ§ Îµ < r) := by smt [h]

example (Îµ Î´ : Real) (h : Îµ â‰  Î´): âˆƒr : Real, (r < Î´ âˆ§ Îµ < r) âˆ¨ (r < Îµ âˆ§ Î´ < r) := by 
  suffices  (âˆƒr : Real, r < Î´ âˆ§ Îµ < r) âˆ¨  (âˆƒr : Real, r < Îµ âˆ§ Î´ < r) by duper [this]
  smt [h]


/- SLIDE

Making of 
=========

Getting all these tools running on one lean project is slightly painful.

You need to check compatible versions on https://reservoir.lean-lang.org/ and
then pin everything in your lakefile.

FIN -/

/- SLIDE
Worthy mentions: 

     https://reservoir.lean-lang.org/@JOSHCLUNE/Hammer - Intended as a common
     frontend to aesop, duper, and additional non-lean provers via lean-auto.

     https://reservoir.lean-lang.org/@verse-lab/veil - A verifier for
     automated and interactive proofs about transition systems. Uses SMT
     solvers and lean in concert to prove things about transition systems. Can
     verify protocols, distributive system behavior. Has a fancy DSL.

FIN -/

